c
c Copyright (c) 1996-2004 by Gennady Serdyuk.  All rights reserved.
c gserdyuk@mail.ru
c 
c Released under GPL v 2.0
c


        
        SUBROUTINE LEN_S
C ***
C *** PROGRAM FOR READING INPUT DATA
C ***
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      include 'charint.i'
      include 'circuit.i'
C       COMMON/NODEL/   NODEEL(500)
C       COMMON/POINT/   MPOINT(500)
C       COMMON/PARAMS/  PARAM (2000)
C       COMMON/POINTR/  NMPNT,NNODE,NPARAM,LENNOD,LENPAR,NNETPR,LENNTP

       COMMON/FRE/        F(2)
       COMMON/BLK1/    KR,KR1,KC,KC1,NNR,NNR1,MN,MN1
       INTEGER         KR(20),KR1(200),KC(10),KC1(20)
       INTEGER         NNR(10),NNR1(20),MN(2,20),MN1(2,200)
       COMMON/BLK2/    KNC,KNR,KN,KNR1,KN1
       COMMON/SERV/    EPSIW,LIMERR,KITU
       COMMON/NEWTON/  EPSSOL,EPSDU,EPSMIN,MAXDU,LIMIT
       DOUBLE PRECISION            EPSSOL,EPSDU,EPSMIN,MAXDU
       COMMON/MODGLB/  MGLOB,IAPR /BLMNI/MNI(2,20),KMNI
       COMMON/TYPVAL/  TYPU,TYPI
       DOUBLE PRECISION            TYPU,TYPI
       COMMON/PRINT /  KPRLEN,KPRSRT,KPRNKR,KPRLIN,KPRSOL,
     +                 KPRVAR,KPRGRF,KPRQUP
       DOUBLE PRECISION   EPS,MU,RO,TGD ,P(50),PAR(1500), YS(1500)
       COMMON/BLKA/    A,KA
       INTEGER*4       A(20,50), KA, KNOT(50)
       

       CHARACTER*4     IT(4), NE, KOH, ITOB, IPAR,  IS, ISTR,           
     +      FNE1,  FNE2, GRAF, YES, NO
       CHARACTER*60    NAME, NNNN
C
C
      COMMON/BLIFF/IFF(7,4),KIFF,NNIFF(4,8),KNNIFF,PNIFF(8),FNE1,FNE2
C
      DATA   IT/4H    ,4H    ,4H    ,4H    /,  IP/6/
      DATA NNNN /'                                                      
     +      '/
      DATA IIN/10/,  KOH/4HEND /,  ITOB/4H    /, IEX/4HEXTR/,           
     +                               IPAR/4H    /
      DATA IS/4H    /, YES/4HYES /, NO/4HNO  /

      NAMELIST/CIRCOM/ EPS,MU,RO,TGD
      NAMELIST/TYP/ IT,KOL,P
      NAMELIST/ELEM/NE,KNOT,IPR,ISTR,IDOP,PAR,IPAR
      NAMELIST/FREQU/ F1,F2,MN,KN
      NAMELIST/SERV/    NAME, GRAF,                EPSIW,LIMERR,KITU, 
     +               EPSSOL,EPSDU,EPSMIN,MAXDU,LIMIT,                KPR
     +LEN,KPRSRT,KPRNKR,KPRLIN,KPRSOL,                KPRVAR,KPRQUP,    
     +            MGLOB,IAPR,                KNC
C
C     ASSIGNMENT OF INITIAL VALUES TO VARIABLES
C
      LENMPO=500
C             LENMPO - MAXIMUM LENGTH OF THE "MPOINT" ARRAY
      LENNOD=500
C             LENNOD - MAXIMUM LENGTH OF THE "NODEEL" ARRAY
      LENPAR=2000
C             LENPAR - MAXIMUM LENGTH OF THE "PARAM" ARRAY
      NNETPR=1
C             NNETPR - ADDRESS OF THE START OF COMMON PARAMETERS
C                      FOR THE ENTIRE CIRCUIT IN "PARAM"
      LENNTP=4
C             LENNTP - NUMBER OF PARAMETERS COMMON TO
C                      THE ENTIRE CIRCUIT
C
C     INITIAL "ZEROING" OF ARRAYS MPOINT, NODEEL, PARAM
      DO 5 II=1,500
      MPOINT(II)=0
      NODEEL(II)=0
   5  CONTINUE
      DO II=1,2000
      PARAM(II)=0.0D0
      ENDDO
C
C     ASSIGNMENT OF INITIAL VALUES
      NNODE=1
C     NNODE - INDEX OF THE FIRST FREE ELEMENT IN "NODEEL"
      NMPOIN=1
C     NMPOIN - INDEX OF THE FIRST FREE ELEMENT IN "MPOINT"
      NPARAM=1
C     NPARAM - INDEX OF THE FIRST FREE ELEMENT IN "PARAM"
      NMPNT=0
C     NMPNT - NUMBER OF PROCESSED DIRECT-TYPE ELEMENTS,
C            I.E., NUMBER OF FILLED ROWS IN "MPOINT"
C
C *** INPUT NAMELIST /SERV/
C            NAME   - NAME OF THE CIRCUIT
C            GRAF   - GRAPHIC MODE SETTING
C            LIMERR - MAXIMUM NUMBER OF ERRORS
C            EPSIW  - FREQUENCY COMPARISON ACCURACY
C            LIMIT  - MAXIMUM NUMBER OF ITERATIONS
C            KPRLEN - PRINT MODE FOR INPUT READING SUBROUTINE
C            KPRSRT - PRINT MODE FOR CIRCUIT NODE SORTING SUBROUTINE
C            KPRNKR - PRINT MODE FOR FREQUENCY MESH GENERATION SUBROUTINE
C            KPRLIN - PRINT MODE FOR PROCESSING LINE SUBCIRCUIT SUBROUTINE
C            KPRSOL - PRINT MODE FOR SOLVING NONLINEAR SYSTEM SUBROUTINE
C            KPRVAR - PRINT MODE FOR PARAMETER VARIATION
C            KPRQUP - PRINT MODE FOR QUALITY ASSESSMENT OF INPUT DATA
C            EPSSOL - REQUIRED SOLUTION ACCURACY
C            EPSDU  - MINIMUM POSSIBLE STEP SIZE
C            EPSMIN - USED FOR LOCAL MINIMUM DETECTION,
C                       NOT CONSIDERED A SOLUTION
C            MAXDU  - DETERMINES MAXIMUM STEP SIZE
C            KITU   - SWITCH FOR INITIAL APPROXIMATION TYPE
C            KNC    - NUMBER OF POINTS FOR UNIFORM DFT EXPANSION
C            MGLOB  - DETERMINES GLOBALIZATION METHOD
C                     0 - NO GLOBALIZATION
C                     1 - LINE SEARCH IN NEWTON DIRECTION
C            IAPR   - ENABLE/DISABLE A PRIORI STEP SIZE LIMITATION METHOD
C                     0 - DISABLED / 1 - ENABLED
C     DEFAULT VALUE ASSIGNMENT
      LIMERR=3
      EPSIW=1.0D-5
      KITU=0

      KPRSRT=0
      KPRNKR=0
      KPRLIN=0
      KPRSOL=0
      KPRLEN=1
      KPRVAR=1
      KPRQUP=1
      KNC=32

      NAME  = NNNN
      GRAF  = NO

C   DEFAULT SETTINGS SEE SUBROUTINE NEINCK
      EPSSOL=1.D-6
      EPSDU=1.0D-6
      EPSMIN=1.0D-7
      MAXDU=0.0D0
      LIMIT=500

      IAPR=0
      MGLOB =0

      TYPI=0.D0
      TYPU=0.D0

C $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
      READ(IIN,SERV)
C $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
 
      IF(KPRLEN.GT.0) WRITE(IP, 6) 
    6 FORMAT(//5X,'C I R C U I T  DESCRIPTION '/         5X,'-----------
     +---------------'/)
      IF(KPRLEN.GT.0.AND.NAME.NE.NNNN)               WRITE(IP, 7) NAME 
    7 FORMAT(5X,A60//)
C     *         5X,'--------------------------'//)

      KPRGRF=0
      IF(GRAF.EQ.YES) KPRGRF=1

C *** INPUT "NAMELIST/CIRKOM/
C             EPS - DIELECTRIC PERMITTIVITY OF THE SUBSTRATE
C             MU  - MAGNETIC PERMEABILITY OF THE CONDUCTOR
C                   MATERIAL
C             RO  - SPECIFIC RESISTANCE OF THE CONDUCTOR
C                   MATERIAL
C             TGD - TANGENT OF THE LOSS ANGLE IN THE DIELECTRIC
C                   SUBSTRATE
C
C
C     DEFAULT VALUE SETTINGS
C
C
      EPS=9.6D0
      MU=1.0D0
      RO=5.7D+07
      TGD=1.D-04
C
C
C $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
      READ(IIN,CIRCOM)
C $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C
C
C AT THIS STAGE, 4 GENERAL PARAMETERS (EPS, MU, RO, TGD)  
C ARE USED FOR THE ENTIRE CIRCUIT, I.E., LENNTP=4. IN THIS CASE, NNETPR=1.
C
      PARAM(NNETPR)=EPS
      PARAM(NNETPR+1)=TGD
      PARAM(NNETPR+2)=MU
      PARAM(NNETPR+3)=RO
C
C
      IF(KPRLEN.GE.0) WRITE(IP, 8)   EPS,MU,RO,TGD
    8 FORMAT(/2X,'   COMMON PARAMETERS OF CKT :'/        5X,'DIELECTRIC 
     +PERMITTIVITY OF SUBSTRATE    ',E12.5/        5X,'MAGNITE PERMITTIV
     +ITY OF CONDUCTORS            = ',        E12.5/        5X,'SPESIFI
     +C RESISTIVITY OF CONDUCTORS           = ',        E12.5/        5X
     +,'tg(delta) - LOSSES IN DIELECTRIC          = ',E12.5)
C
C *********************************************************
C *** CALLING THE SUBROUTINE CONTAINING THE ARRAY "A"
      CALL LENA
C *********************************************************
C
C
C
C     INCREASE THE COUNTER NPARAM BY THE NUMBER OF COMMON PARAMETERS
      NPARAM=NPARAM+LENNTP
C
C *** INPUT NAMELIST/TYP/ IT, KOL, P
C          IT - ELEMENT TYPE
C          KOL - NUMBER OF ELEMENTS OF THIS TYPE
C          P  - COMMON PARAMETERS OF THE TYPE (IF THEY EXIST)
C
   10 CONTINUE
      KOL=1
      DO 13 IPP=1,50
      P(IPP)=0.D0
   13 CONTINUE
      DO IPP=1,4
      IT(IPP)=ITOB
      ENDDO
C
C $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
      READ(IIN,TYP)
C $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C
      IF(IT(1).EQ.KOH) GOTO 110
      NMPNT=NMPNT+1
C
C     OUTPUT OF INFORMATION FOR NAMELIST/TYP/
      IF(KPRLEN.GT.0)WRITE(IP, 9) (IT(ITN),ITN=1,4)
    9 FORMAT(/1X,'ELEMENT TYPE - ',4A4)
C
C     DETERMINATION OF THE ROW NUMBER (IA) IN ARRAY "A",
C     WHICH CORRESPONDS TO THE PROCESSED ELEMENT TYPE
      DO 11 IA=1,KA
      IF(C2I(IT(1)).EQ.A(1,IA).AND.C2I(IT(2)).EQ.A(2,IA).AND.   C2I(IT(3
     +)).EQ.A(3,IA).AND.C2I(IT(4)).EQ.A(4,IA)) GOTO12
      IF(IA.EQ.KA) GOTO 200
   11 CONTINUE
C
C     IS THERE ENOUGH SPACE IN THE "MPOINT" ARRAY TO 
C     ADD A NEW ELEMENT TYPE?
      NNNNN=NMPOIN+20
      IF(NNNNN.GT.LENMPO) GOTO 300
C
C *** FILLING THE "MPOINT" ARRAY  ************************************
C
   12 CONTINUE
      NM=NMPOIN-1
C       1-4 - NAME OF ELEMENT TYPE (NO MORE THAN 16 CHARACTERS;
C           THE FIRST FOUR CHARACTERS ARE KEY)
      MPOINT(NM+1)=A(1,IA)
      MPOINT(NM+2)=A(2,IA)
      MPOINT(NM+3)=A(3,IA)
      MPOINT(NM+4)=A(4,IA)
C       5 - NUMBER OF ELEMENTS OF THIS TYPE
      MPOINT(NM+5)=KOL
C       6 - FLAG DETERMINING THE VARIETY OF THE ELEMENT
      MPOINT(NM+6)=A(6,IA)
C       7 - LENGTH OF THE DESCRIPTION STRING FOR A SINGLE ELEMENT 
C           OF THIS TYPE IN "NODEEL"
      MPOINT(NM+7)=A(7,IA)
C       8 - NUMBER OF NODES IN THE MATHEMATICAL MODEL
C           (FOR LINEAR POLYNOMIALS, I.E., A(6,IA)=2,
C           MPOINT STORES 0 BECAUSE THIS VALUE IS DETERMINED
C           IN "NODEEL" SEPARATELY FOR EACH LINEAR POLYNOMIAL)
      IF(A(6,IA).EQ.2) MPOINT(NM+8)=0
      IF(A(6,IA).NE.2) MPOINT(NM+8)=A(8,IA)
C       9 - NUMBER OF INTERNAL NODES AUTOMATICALLY NUMBERED
C           WHEN DESCRIBING THE CONNECTION NODES OF THE ELEMENT
      MPOINT(NM+9)=A(9,IA)
C      10 - ADDRESS OF THE START OF ELEMENT DESCRIPTIONS OF THIS
C           TYPE IN "NODEEL"
      MPOINT(NM+10)=NNODE
C      11 - NUMBER OF PARAMETERS COMMON TO THIS ELEMENT TYPE
      MPOINT(NM+11)=A(11,IA)
C      12 - ADDRESS OF THE START OF THE LIST OF PARAMETERS 
C           COMMON TO THIS ELEMENT TYPE IN THE "PARAM" ARRAY
      MPOINT(NM+12)=NPARAM
C      13 - NUMBER OF PARAMETERS DESCRIBING THE MATHEMATICAL
C           MODEL OF THE ELEMENT (ADDITIONAL ANALOGUE POSITION N8)
      IF(A(6,IA).EQ.2) MPOINT(NM+13)=A(13,IA)
      IF(A(6,IA).NE.2) MPOINT(NM+13)=A(13,IA)
C      14 - ADDRESS OF THE START OF THE PARAMETER LIST FOR 
C           DIFFERENT VARIANTS OF MATHEMATICAL MODELS 
C           OF THIS ELEMENT TYPE IN "PARAM"
      MPOINT(NM+14)=A(11,IA)+NPARAM
C      15 - FLAG FOR PARAMETER VARIATION OF ELEMENTS
C           FILLED WHEN A VARIABLE ELEMENT ARRIVES.
C           INITIAL VALUE IS 0, STORED IN ARRAY "A"
      MPOINT(NM+15)=A(15,IA)
C      16 - ADDRESS OF THE LIST OF THE FULL NAME OF THE ELEMENT TYPE
C           AND ITS OUTPUTS (NOT USED YET)
      MPOINT(NM+16)=A(16,IA)
C      17 - ADDRESS OF THE START OF THE LIST OF FULL NAMES 
C           OF ELEMENT PARAMETERS (NOT USED YET)
      MPOINT(NM+17)=A(17,IA)
C      18 - MAXIMUM NUMBER OF INPUT QUANTITIES WHEN CALCULATING
C           THE CURRENT OF A NONLINEAR ELEMENT
C           (FOR OTHER TYPES, THIS PARAMETER = 0)
      MPOINT(NM+18)=A(18,IA)
C      19 - MAXIMUM NUMBER OF OUTPUT CURRENTS DURING THE ANALYSIS
C           OF A NONLINEAR ELEMENT
C           (FOR OTHER TYPES, THIS PARAMETER = 0)
      MPOINT(NM+19)=A(19,IA)
C      20 - ADDITIONAL FLAG (RESERVED)
      MPOINT(NM+20)=A(20,IA)
C
C     CONTROL PRINTING OF THE "MPOINT" ARRAY ROW
      NM1=NM+1
      NM20=NM+20
      IF(KPRLEN.GT.2)WRITE(IP, 14)  (MPOINT(NN),NN=NM1,NM20)
   14 FORMAT('  MPOINT= ',4A4,16(1X,I3)/)
C
      NMPOIN=NM+20+1
C
C *** FORMULATION OF THE "PARAM" ARRAY FOR GENERAL PARAMETERS TYPE *****
C
      KOLC=A(11,IA)
      DO 15 IKOL=1,KOLC
      NKOLC=NPARAM-1+IKOL
      IF(KPRLEN.GE.3) WRITE(IP, 1513) KOLC, IKOL, NPARAM, NKOLC, P(IKOL)
 1513 FORMAT(2X,'KOLC=',I3,' IKOL=',I3,' NPARAM=',I4,' NKOLC=',I4,      
     +    ' P(IKOL)=',E12.5)
      PARAM(NKOLC)=P(IKOL)
      IF(KPRLEN.LE.3) GOTO 15
      WRITE(IP, 1515) NKOLC,PARAM(NKOLC)
 1515 FORMAT(2X,'COMM PARAM(',I4,')=',E12.5)
   15 CONTINUE
C     OUTPUT INFORMATION ABOUT GENERAL PARAMETERS TYPE
      NPKOLC=NPARAM-1+KOLC
      DO 16 N=NPARAM,NPKOLC
      B=0.1D-15
      IF(PARAM(N).GE.B.OR.PARAM(N).LE.-B) GOTO 17
   16 CONTINUE
      GOTO 19
   17 CONTINUE
      IF(KPRLEN.GE.1)WRITE(IP, 18) (PARAM(NKOLC),NKOLC=NPARAM,NPKOLC)
   18 FORMAT(22X,'COMM PARAM=',3(E12.5,', ')/       34X,3(E12.5,', ')/)
   19 CONTINUE
      NPARAM=NPARAM+KOLC
C
C *** PROCESSING OF ALL ELEMENTS OF ONE TYPE ***************************
C
C     FIRST, WE CHECK KOL, GIVEN IN THE INPUT DATA
      IF(KOL.LT.1) GOTO 370
C
C
      DO 100 IK=1,KOL
C
C     INITIAL VALUE ASSIGNMENT FOR ELEMENTS  
C     OF THE SAME TYPE  
      DO 4  I=1,50
      KNOT(I)=-77777
    4 CONTINUE
      DO 20  I=1,1500
      PAR(I)=0.5D+35
      YS(I) =0.5D+35 
   20 CONTINUE
      IPR=0
      ISTR=IS
      IDOP=0
      IPAR=ITOB
C
C *** INPUT NAMELIST /ELEM/ NE, KNOT, IPR, ISTR, IDOP, PAR, IPAR
C          NE   - SYMBOLIC DESIGNATION OF THE ELEMENT  
C                 (4 CHARACTERS)  
C          KNOT - NODE NUMBERS OF THE SCHEME  
C          IPR  - PARAMETER VARIATION INDICATOR OF THE ELEMENT  
C          ISTR - TYPE OF SEMICONDUCTOR STRUCTURE  
C                 (ONLY FOR NONLINEAR ELEMENTS)  
C          IDOP - GROUP MEMBERSHIP NUMBER  
C          PAR  - PARAMETERS SPECIFIC TO A PARTICULAR ELEMENT  
C          IPAR - DESIGNATION OF AN ELEMENT WHOSE  
C                 PARAMETERS ARE IDENTICAL TO THOSE  
C                 OF ELEMENT NE  
C
C
C  $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$  
      READ(IIN,ELEM)
C  $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C
C
      IF(KPRLEN.GE.1)WRITE(IP, 21) NE
   21 FORMAT(6X,'ELEMENT - ',A4,':')
C
C     CHECKING THE DESIGNATIONS OF THE PROCESSED ELEMENTS  
      DO 22 IL=1,NMPNT
      IF(C2I(NE).EQ.MPOINT(1+(NMPNT-1)*20)) GOTO 240
   22 CONTINUE
      DO 23 I=1,NNODE
      IF(C2I(NE).EQ.NODEEL(I)) GOTO 240
   23 CONTINUE
C
C     DETERMINATION OF THE NUMBER OF NODES (KOLPOL)  
C
      DO 255 I=1,50
      IF(KNOT(I).GT.-77770) KOLPOL=I
      IF(KOLPOL.EQ.I.AND.KPRLEN.GE.3)     WRITE(IP,24) I, KNOT(I), KOLPO
     +L
  24  FORMAT(2X,' KNOT(',I3,')=',I6,'       KOLPOL=',I3)
 255  CONTINUE
C
C  &&&&  INPUT OF PARAMETERS SPECIFIED VIA CHANNEL 8  &&&&&&&&&&  
      IF(C2I(IPAR).NE.IEX) GOTO 27
      OPEN(8,FILE='SNTPY',STATUS='OLD')
      READ(8,*,END=259,ERR=259) (YS(I),I=1,27)
  259 CONTINUE
      IF(KPRLEN.GE.3) WRITE(IP,25) (I,YS(I),I=1,27)
   25 FORMAT(2X,'YS(',I4,')=',E13.6)  
      DO 26 I=1,1500
      PAR(I)=YS(I)
   26 CONTINUE
C  &&&&  END OF INPUT  &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&  
C  
C     DETERMINATION OF THE NUMBER OF PARAMETERS (KOLPAR)  
C     RECEIVED AT THE INPUT  
   27 CONTINUE  
      DO 29 I=1,1500
      IF(PAR(I).LT.0.5D+34)             KOLPAR=I
      IF(KOLPAR.EQ.I.AND.KPRLEN.GE.3) WRITE(IP, 28) I,PAR (I),KOLPAR
  28  FORMAT(2X,'  PAR(',I4,')=',E12.5,' KOLPAR=',I4)
  29  CONTINUE
C
C
C     DEPENDING ON THE TYPE OF ELEMENT, THE INITIAL  
C     DATA IS PROCESSED DIFFERENTLY  
      IF(A(6,IA).EQ.1) GOTO 30
      IF(A(6,IA).EQ.2) GOTO 40
      IF(A(6,IA).EQ.3) GOTO 50
      IF(A(6,IA).EQ.5) GOTO 8000
C
C === FORMATION OF THE "NODEEL" STRING FOR LINEAR DIODES  
C
   30 CONTINUE
C       WHETHER THE PLACE IN THE "NODEEL" STRING IS SUFFICIENT  
C       FOR THE RECORD OF A NEW ELEMENT  
      NNNNN=NNODE+5
      IF(NNNNN.GT.LENNOD) GOTO 350
C
C     CHECKING THE NUMBER OF CONNECTION NODES AND PARAMETERS  
      IF(KOLPOL.NE.2) GOTO 320
      IF(KOLPAR.NE.A(13,IA)) GOTO 330
C
      NODEEL(NNODE)=C2I(NE)
      IF(NNNNN.GT.LENNOD) GOTO 350
      NODEEL(NNODE+1)=KNOT(1)
      NODEEL(NNODE+2)=KNOT(2)
C       KNOT(1),KNOT(2) - Yúìù BKìàþEHéñ
      NODEEL(NNODE+3)=IPR
C       IPR - VARIATION INDICATOR. MAY BE ABSENT  
C             IN THE "NAMELIST" LIST  
      IF(IPR.EQ.1) MPOINT(NM+15)=1  
      NODEEL(NNODE+4)=IDOP  
C       IDOP - GROUP MEMBERSHIP NUMBER. MAY  
C              BE ABSENT IN THE "NAMELIST" LIST.  
C
      IF(KPRLEN.GE.1)WRITE(IP, 33) KNOT(1),KNOT(2)
   33 FORMAT(22X,'Yúìù BKì.=',I2,',',I2)
C
C     CONTROL PRINTING OF THE "NODEEL" STRING  
      IF(KPRLEN.LE.2) GOTO 36
      NNODE4=NNODE+4
      NNODE5=NNODE+5
      WRITE(IP, 34)  NNODE
  34  FORMAT(2X,'NODEEL(',I3,')=')
      WRITE(IP, 35) (NODEEL(N),N=NNODE,NNODE4),NNODE5
  35  FORMAT(15X,A4,4(2X,I4),2X,'NNODE CB.=',I3)
  36  CONTINUE
C
      NNODE=NNODE+5
      GOTO 60
C
C === FORMATION OF THE "NODEEL" STRING FOR LINEAR  
C === MULTIPLEXERS  
C
   40 CONTINUE
C     IS THERE ENOUGH SPACE IN THE "NODEEL" ARRAY?  
      NNNNN=NNODE+8+KOLPOL
      IF(NNNNN.GT.LENNOD) GOTO 350
      NODEEL(NNODE)=C2I(NE)
C       NE - SYMBOLIC DESIGNATION OF THE ELEMENT  
C     THE NUMBER OF TERMINALS IS STORED IN "A(8,IA)"  
      IF(KOLPOL.NE.A(8,IA)) GOTO 320  
      NODEEL(NNODE+1)=KOLPOL  
C     THE NUMBER OF PARAMETERS DESCRIBING THE ELEMENT  
C     AT A SINGLE FREQUENCY POINT IS STORED IN "A(13,IA)"  
      IF(KOLPAR.NE.A(13,IA)) GOTO 330  
      NODEEL(NNODE+2)=KOLPAR  
C     INDICATOR DESCRIBING THE CLASS OF THE ELEMENT  
C     (ACTIVE OR PASSIVE)  
C     STORAGE LOCATION IN "A(20,IA)"  
      NODEEL(NNODE+3)=A(20,IA)  
C     CONNECTION NODES  
      DO 41 IKN=1,KOLPOL
      NODEEL(NNODE+3+IKN)=KNOT(IKN)
   41 CONTINUE
      IKN=KOLPOL
C     SYMBOLIC DESIGNATION OF THE PARAMETER SET  
C     (AT THIS STAGE, THE SYMBOLIC DESIGNATION  
C     OF THE ELEMENT IS RECORDED, I.E., "NE")  
      NODEEL(NNODE+4+IKN)=C2I(NE)  
C     THE ADDRESS OF THE PARAMETER SET IN "PARAM" IS CALCULATED  
      IADR=MPOINT(NM+14)+(IK-1)*MPOINT(NM+13)
      IADR=NPARAM
      NODEEL(NNODE+5+IKN)=IADR
      NODEEL(NNODE+6+IKN)=IPR
C       IPR - VARIATION INDICATOR. MAY BE ABSENT  
C            IN THE "NAMELIST" LIST  
      IF(IPR.EQ.1) MPOINT(NM+15)=1
      NODEEL(NNODE+7+IKN)=IDOP
C       IDOP - GROUP MEMBERSHIP NUMBER. MAY  
C              BE ABSENT IN THE "NAMELIST" LIST.  
C  
C     PRINTING INFORMATION ABOUT "NAMELIST/ELEM/"  
C
      IF(KPRLEN.GE.1)WRITE(IP, 44) (KNOT(JJ),JJ=1,KOLPOL)
   44 FORMAT(22X,'NODES   .=',10(I2,','))
C
C     CONTROL PRINTING OF THE "NODEEL" STRING  
      IF(KPRLEN.LE.2) GOTO 48
      NNODE3=NNODE+3+KOLPOL
      WRITE(IP, 45) NNODE
   45 FORMAT(2X,'NODEEL(',I3,')=')
      WRITE(IP,46) (NODEEL(NN),NN=NNODE,NNODE3)
   46 FORMAT(15X,A4,10(1X,I4))
      NNODE4=NNODE+4+KOLPOL
      NNODE7=NNODE+7+KOLPOL
      NNODE8=NNODE+8+KOLPOL
      WRITE(IP, 47) (NODEEL(NN),NN=NNODE4,NNODE7),NNODE8
   47 FORMAT(17X,A4,3(1X,I4),'  NNODE CB.=',I3)
   48 CONTINUE
C
      NNODE=NNODE+8+KOLPOL
C
      GOTO 60
C
C
C === FORMATION OF THE "NODEEL" STRING FOR ELEMENTS  
C === SPECIFIED USING Y- OR S-MATRICES  
C
 8000 CONTINUE
      NNNNN=NNODE+8+KOLPOL
      IF(NNNNN.GT.LENNOD) GOTO 350
      NODEEL(NNODE)=C2I(NE)
C       NE - SYMBOLIC DESIGNATION OF THE ELEMENT  
C     THE NUMBER OF TERMINALS FOR EACH ELEMENT IS DETERMINED AUTOMATICALLY.  
      NODEEL(NNODE+1)=KOLPOL  
C     THE NUMBER OF PARAMETERS IS DETERMINED AUTOMATICALLY FOR EACH ELEMENT  
C     AUTOMATICALLY CHECKED  
      NODEEL(NNODE+2)=KOLPAR  
C  ?? INDICATOR, DESCRIBING THE TYPE OF MATRIX (Y OR S) ??  
      NODEEL(NNODE+3)=A(20,IA)  
C     CONNECTION NODES  
      DO 8010 IKN=1,KOLPOL
      NODEEL(NNODE+3+IKN)=KNOT(IKN)
 8010 CONTINUE
      IKN=KOLPOL
C     SYMBOLIC DESIGNATION OF THE PARAMETER SET  
C     (AT THIS STAGE, THE SYMBOLIC DESIGNATION  
C      OF THE ELEMENT IS RECORDED, I.E., "NE")  
      NODEEL(NNODE+4+IKN)=C2I(NE)  
C     THE ADDRESS OF THE PARAMETER SET IN "PARAM"  
      NODEEL(NNODE+5+IKN)=NPARAM  
C !!! VARIATION INDICATOR (OPTIONAL OPERATION)  
      NODEEL(NNODE+6+IKN)=IPR  
C !!! IF A VARIATION INDICATOR IS SET, THEN IT IS NECESSARY  
C !!! TO UPDATE ADDITIONAL INFORMATION IN "MPOINT"  
      IF(IPR.EQ.1) MPOINT(NM+15)=1  
C !!! GROUP MEMBERSHIP NUMBER (OPTIONAL OPERATION)  
      NODEEL(NNODE+7+IKN)=IDOP
C
C     PRINTING INFORMATION ABOUT "NAMELIST/ELEM/"  
C
      IF(KPRLEN.GE.1)   WRITE(IP, 8044) (KNOT(JJ),JJ=1,KOLPOL)
 8044 FORMAT(22X,'NODES    =',10(I2,','))
C
C     CONTROL PRINTING OF THE "NODEEL" STRING  
      IF(KPRLEN.LE.2) GOTO 8048
      NNODE3=NNODE+3+KOLPOL
      WRITE(IP, 8045) NNODE
 8045 FORMAT(2X,'NODEEL(',I3,')=')
      WRITE(IP, 8046) (NODEEL(NN),NN=NNODE,NNODE3)
 8046 FORMAT(15X,A4,10(1X,I3))
      NNODE4=NNODE+4+KOLPOL
      NNODE7=NNODE+7+KOLPOL
      NNODE8=NNODE+8+KOLPOL
      WRITE(IP, 8047) (NODEEL(NN),NN=NNODE4,NNODE7),NNODE8
 8047 FORMAT(17X,A4,3(1X,I4),'  NNODE CB.=',I3)
 8048 CONTINUE
C
      NNODE=NNODE+8+KOLPOL
C
      GOTO 60
C
C
C
C
C === FORMATION OF THE "NODEEL" STRING FOR NONLINEAR  
C === ELEMENTS  
C
   50 CONTINUE
C
C     IS THERE ENOUGH SPACE IN THE ARRAY "NODEEL"
      NNNNN=NNODE+7+KOLPOL+A(9,IA)
      IF(NNNNN.GT.LENNOD) GOTO 350
C
      NODEEL(NNODE)=C2I(NE)
C       NE - SYMBOLIC DESIGNATION OF THE ELEMENT  
C     THE TOTAL NUMBER OF NODES IS STORED IN "A(8,IA)"  
C     THE NUMBER OF EXTERNAL NODES IS EQUAL TO THE DIFFERENCE  
C     BETWEEN THE TOTAL NUMBER OF NODES AND THE NUMBER OF INTERNAL NODES  
      KOLUZ=A(8,IA)-A(9,IA)
C     CHECKING THE NUMBER OF EXTERNAL NODES  
      IF(KOLPOL.NE.KOLUZ) GOTO 320  
C     NODE NUMBERS TO WHICH THE EXTERNAL  
C     ELECTRODES OF THE ELEMENT ARE CONNECTED  
      DO 51 IKN=1,KOLUZ
      NODEEL(NNODE+IKN)=KNOT(IKN)
   51 CONTINUE
C     THE NUMBER OF INTERNAL NODES IS STORED IN "A(9,IA)"  
      KOLVN=A(9,IA)
C     IF KOLVN=0, THEN INTERNAL NODES ARE NOT CONSIDERED  
      IF(KOLVN.EQ.0) GOTO 556
C     NUMBERS OF INTERNAL NODES  
      IKV1=1+KOLUZ
      IKV2=KOLUZ+KOLVN
      DO 56 IKV=IKV1,IKV2
      NODEEL(NNODE+IKV)=100+IKV
      KNOT(IKV)=100+IKV
   56 CONTINUE
  556 IF(KOLVN.EQ.0) IKV=KOLUZ
      IF(KOLVN.NE.0) IKV=IKV2
C     SYMBOLIC DESIGNATION OF THE PARAMETER SET  
C     (AT THIS STAGE, THE SYMBOLIC DESIGNATION  
C     OF THE ELEMENT TYPE IS STORED, I.E., "IT(1)", "IT(2)")  
      NODEEL(NNODE+1+IKV)=C2I(IT(1))  
      NODEEL(NNODE+2+IKV)=C2I(IT(2))  
C     THE ADDRESS OF THE PARAMETER SET IN "PARAM" IS CALCULATED  
      IADR=NPARAM
      NODEEL(NNODE+3+IKV)=IADR
      NODEEL(NNODE+4+IKV)=IPR
C       IPR - VARIATION FLAG. MAY BE ABSENT IN  
C             THE "NAMELIST" LIST  
      IF(IPR.EQ.1) MPOINT(NM+15)=1
C     IF(ISTR.EQ.IS) GOTO 260  *** No longer needed 3.11.89 ***
      NODEEL(NNODE+5+IKV)=C2I(ISTR)
C       ISTR - TYPE OF SEMICONDUCTOR STRUCTURE  
C              (ONLY FOR NONLINEAR ELEMENTS)  
      NODEEL(NNODE+6+IKV)=IDOP
C       IDOP - NODE GROUP MEMBERSHIP FLAG. MAY  
C              BE ABSENT IN THE "NAMELIST" LIST  
C      CHECKING THE NUMBER OF PARAMETERS  
       IF(KOLPAR.NE.A(13,IA)) GOTO 330
C
C
C     PRINTING INFORMATION FOR "NAMELIST/ELEM/"  
C
      IF(KPRLEN.GE.1)WRITE(IP, 53) (KNOT(JJ),JJ=1,KOLUZ)
   53 FORMAT(22X,'NODES    =',10(I2,','))
C
      IF(KPRLEN.GT.1.AND.KOLVN.GT.0)WRITE(IP, 52) (KNOT(JJ),JJ=IKV1,IKV2
     +)
   52 FORMAT(22X,'INTERN NODS=',10(I3,',')/       34X,10(I3,','))
C
      IF(KPRLEN.GE.1.AND.ISTR.NE.IS)WRITE(IP, 54) ISTR
   54 FORMAT(22X,'STRUCTURE - ',A4)
C
C     CONTROLLED PRINTING OF THE "NODEEL" LINE  
      IF(KPRLEN.LE.2) GOTO 55
      NNODEK=NNODE+KOLUZ
      WRITE(IP, 555) NNODE
  555 FORMAT(2X,'NODEEL(',I3,')=')
      WRITE(IP, 57) (NODEEL(NN),NN=NNODE,NNODEK)
   57 FORMAT(15X,A4,10(1X,I4))
      IF(KOLVN.EQ.0) GOTO 558
      NNODEK=NNODE+IKV1
      NNODEL=NNODE+IKV2
      WRITE(IP, 58) (NODEEL(NN),NN=NNODEK,NNODEL)
   58 FORMAT(15X,10(1X,I4))
  558 CONTINUE
      NNODE1=NNODE+1+IKV
      NNODE6=NNODE+6+IKV
      NNODE7=NNODE+7+IKV
      WRITE(IP, 59) (NODEEL(NN),NN=NNODE1,NNODE6),NNODE7
   59 FORMAT(15X,A4,A4,2(1X,I4),1X,A4,1X,I4,        ' NNODE CB.=',I3)
   55 CONTINUE
C
      NNODE=NNODE+7+IKV
C
   60 CONTINUE
C
C     MESSAGE ABOUT GROUP MEMBERSHIP  
C     (PRINTED IF THE ELEMENT IS EXTERNAL)  
      IF(KPRLEN.GE.1.AND.IDOP.EQ.1) WRITE(IP, 660)
  660 FORMAT(22X,'ELEMENT -EXTERNAL')
C
C === IN CASE OF OUTPUTTING A LIST OF PARAMETERS AND  
C === THE PRESENCE OF A LINK TO ONE OF THE PREVIOUS  
C === ELEMENTS:  
      IF(IPAR.EQ.ITOB) GOTO 64
      IF(KPRLEN.GE.3) WRITE(IP,670) IPAR,IEX
  670 FORMAT(2X,'IPAR=',A4,' IEX=',A4)
      IF(C2I(IPAR).EQ.IEX) GOTO 64
C     (FOR NONLINEAR DOUBLE-CURRENT ELEMENTS, THIS IS NOT ALLOWED)  
      IF(A(6,IA).EQ.1) GOTO 280  
C     (SAME AS FOR ELEMENTS DEFINED BY THE MATRIX)  
      IF(A(6,IA).EQ.4) GOTO 340
C
C     DETERMINING THE ADDRESS OF THE START OF THE DESCRIPTION OF  
C     ELEMENTS OF THE GIVEN TYPE IN "NODEEL"  
      NMP=NMPOIN-21  
      NADR=MPOINT(NMP+10)  
C     DETERMINING THE LENGTH OF THE DESCRIPTION LINE FOR ONE  
C     ELEMENT OF THE GIVEN TYPE  
      NDL=A(7,IA)  
C     CHECKING FOR THE PRESENCE OF THE ELEMENT, SPECIFIED  
C     IN THE LINK  
      DO 61 NN=1,KOL
      NND=NADR+NDL*(NN-1)
      IF(C2I(IPAR).EQ.NODEEL(NND)) GOTO 62
      IF(KPRLEN.GE.3) WRITE(IP,680) NN,KOL
  680 FORMAT(2X,'NN=',I4,' KOL=',I4)
      IF(NN.EQ.KOL) GOTO 220
   61 CONTINUE
   62 CONTINUE
C     DETERMINING THE ADDRESS OF THE START OF THE PARAMETER LIST  
C     IN THE ARRAY "PARAM", LINKED TO THE ARRAY  
C     "NODEEL", FOR THE ELEMENT SPECIFIED IN THE LINK.  
      IF(A(6,IA).EQ.2) NIADR=NND+5+A(8,IA)
      IF(A(6,IA).EQ.3) NIADR=NND+3+A(8,IA)
      IADR=NODEEL(NIADR)
C     WRITING INTO THE ARRAY "NODEEL" THE NEW VALUE  
C     OF THE ADDRESS FOR THE PROCESSED ELEMENT.  
      NNN=NNODE-NDL
      IF(A(6,IA).EQ.2) NNDA=NNN+5+IKN
      IF(A(6,IA).EQ.3) NNDA=NNN+3+IKV
      NODEEL(NNDA)=IADR
C     PRINT 6666,NND,NIADR,IADR,NNN,NNODE,IKV
C6666 FORMAT(2X,'NND,NIADR,IADR,NNN,NNODE,IKV=',6I4)
      IF(KPRLEN.GE.2) WRITE(IP, 6262) NNDA,IADR
 6262 FORMAT(2X,'NEW VALUE  "NODEEL(',I4,')"=',I4)
      IF(KPRLEN.GE.1)WRITE(IP, 66) IPAR
   66 FORMAT(22X,'PARAMETERS ARE IDENTICAL TO PAR',           'OF ELEM. 
     +',A4)
      GOTO 100
   64 CONTINUE
C
C *** FORMATTING THE ARRAY "PARAM"  ***************************************
C  
C     PRELIMINARY CHECK. VERIFY IF THE LOCATION IN "PARAM" IS VALID  

      NNNNN=NPARAM+KOLPAR
      IF(NNNNN.GT.LENPAR) GOTO 360

      F1=0.D0
      F2=0.D0

      DO 75 JF=1,KIFF
      IF(KPRLEN.GE.3) WRITE(IP,69) JF
   69 FORMAT(2X,'   JF=',I3)
      JJF=-1
      IF(IFF(1,JF).EQ.C2I(IT(1)).AND.IFF(2,JF).EQ.C2I(IT(2)).AND.   IFF(
     +3,JF).EQ.C2I(IT(3)).AND.IFF(4,JF).EQ.C2I(IT(4))) GOTO 70
C      IF(IFF(1,JF).EQ.IT(1).AND.IFF(2,JF).EQ.IT(2).AND.
C     *   IFF(3,JF).EQ.IT(3).AND.IFF(4,JF).EQ.IT(4)) JJF=JF
      GO TO 73
  70  CONTINUE
      IFF(6,JF)=JF
      JJF=JF
      IF(KPRLEN.GE.3) WRITE(IP,71) JF,(IFF(JJF1,JF),JJF1=1,7)
  71  FORMAT(2X,'IFF(   ,',I3,')=',4A4,3(1X,I3))

  73  CONTINUE
      DO 85 IKOL=1,KOLPAR
      NI=NPARAM-1+IKOL
      PARAM(NI)=PAR(IKOL)
      IF(JJF.EQ.-1) GOTO 85
      DO 83 KF=1,KIFF
C     IF(KPRLEN.GT.3) WRITE(IP,76) IFF(6,KF),KF,IKOL,IFF(5,KF)
C  76 FORMAT(2X,'IFF(6,KF)=',I3,' KF=',I3,' IKOL=',I3,' IFF(5,KF)=',I3)
      IF(IFF(6,KF).EQ.JJF.AND.IKOL.EQ.IFF(5,KF)) GOTO 77
      GOTO 83
   77 CONTINUE
      KNNIFF=KNNIFF+1
      NNIFF(1,KNNIFF)=C2I(NE)
      NNIFF(2,KNNIFF)=JJF
      NNIFF(3,KNNIFF)=NI
      IF(KPRLEN.GE.3) WRITE(IP,80) KNNIFF, NE, KNNIFF, JJF,             
     +                KNNIFF, NI, KF, NI, PARAM(NI)
   80 FORMAT(2X,'NNIFF=(1',I3,')= NE  = ',A4/       2X,'NNIFF=(2',I3,')=
     + JJF =',I3/       2X,'NNIFF=(3',I3,')= NI  =',I3/       2X,' KF=',
     +I3,' PARAM(',I4,')=',E12.5)

      GOTO 85
   83 CONTINUE
   85 CONTINUE
   75 CONTINUE
C     CONTROLLED PRINTING OF "PARAM"  
      NPARA1=NPARAM-1+KOLPAR
      IF(KPRLEN.GE.1)WRITE(IP, 90) (PARAM(NN),NN=NPARAM,NPARA1)
   90 FORMAT(22X,'PARAMETRS=',4(E12.5,',')/(32X,4(E12.5,',')))
C    *      (32X,4(E12.5,','))/(32X,4(E12.5,','))/(32X,4(E12.5,',))/)
      IF(KPRLEN.GE.3) WRITE(IP, 95) NPARAM,NPARA1
   95 FORMAT(22X,'IN ARRAY  "PARAM" FILLED ',           'POS. SINCE',I4,
     +' TO ',I4)
C
      NPARAM=NPARAM+KOLPAR
      NNNP=NPARAM
  100 CONTINUE
C
C *** END OF NAMELIST/TYP/ AND NAMELIST/ELEM/ INPUT  
C
      GOTO 10
C
C
  110 CONTINUE
C
C
C *********************************************************
C *** CALL TO THE FREQUENCY PROCESSING SUBROUTINE  
      KPR=KPRLEN
      CALL FREQUEN(NF3,KPR)
C *********************************************************
C
      F1OLD=F(1)
      F2OLD=F(2)
      F1   =F(1)
      F2   =F(2)
C
C *** READING NAMELIST/FREQU/ F1,F2,MN,KN  
C                     F1 - LOWER FREQUENCY  
C                     F2 - UPPER FREQUENCY  
C                     MN - COMBINATION COEFFICIENT MATRIX  
C                     KN - NUMBER OF COMBINATIONS  
C
C $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
      READ(IIN,FREQU)
C $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C
C
C     ASSIGNING THE VALUES OF F1 AND F2 TO THE ARRAY F  
      F(1)=F1
      F(2)=F2
C
C     COPYING THE ARRAY MN TO THE ARRAY MNI FOR ITS RENAMING  
      DO 118 J118=1,KN
      MNI(1,J118)=MN(1,J118)
      MNI(2,J118)=MN(2,J118)
  118 CONTINUE
      KMNI=KN
C
C     PRINTING INFORMATION FROM NAMELIST/FRE/  

      IF(KPRLEN.GE.1.AND.F1OLD.NE.F1) WRITE(IP, 120) F1
  120 FORMAT(2X,'FUNDAMENTAL FR.  F1 =',E12.5,',')

      IF(KPRLEN.GE.1.AND.F1OLD.EQ.F1) WRITE(IP, 121) F1, FNE1
  121 FORMAT(2X,'FUNDAMENTAL FR.  F1 =',E12.5,' ( ELEMENT ',A4,' )')

      IF(KPRLEN.GE.1.AND.F2OLD.NE.F2) WRITE(IP, 122) F2
  122 FORMAT(2X,'FUNDAMENTAL FR.  F2 =',E12.5,',')

      IF(KPRLEN.GE.1.AND.F2OLD.EQ.F2.AND.FNE2.NE.IS)                    
     +             WRITE(IP, 123) F2, FNE2
  123 FORMAT(2X,'FUNDAMENTAL FR.  F2 =',E12.5,' ( ELEMENT ',A4,' )')

      IF(KPRLEN.GE.1.AND.F2OLD.EQ.F2.AND.FNE2.EQ.IS)                    
     +            WRITE(IP, 122) F2
      IF(KPRLEN.GE.1.AND.NF3.EQ.1)    WRITE(IP, 124)
  124 FORMAT(//2X,'A T T E N T  O N: MORE THAN 2 FREQUENCIES  !'/       
     + /2X,' MAKE CHANGES IN INPUT TASK !')
     
      IF(NF3.EQ.1) STOP

      IF(KPRLEN.GE.1) WRITE(IP, 125) 
  125 FORMAT(2X,'FREQUENCY GRID   ( F = mi*F1 + ni*F2 ) ')
      DO 127 IZ=1,KN
      FS=MN(1,IZ)*F(1)+MN(2,IZ)*F(2)
      IF(KPRLEN.GE.1) WRITE(IP, 126) IZ,MN(1,IZ), IZ,MN(2,IZ), FS
  126 FORMAT(12X,'m',I2,' =',I3,',   n',I2,' =',I3,'   F = ',E13.6)
  127 CONTINUE

C     CONSIDERING THE CASE OF TWO ZERO FREQUENCIES  
      IF(F(1).NE.0.D0.OR.F(2).NE.0.D0) GOTO 129
      MN(1,1) = 0
      MN(2,1) = 0
      MNI(1,1)= 0
      MNI(2,1)= 0
      KN=1
      IF(KPRLEN.GE.1) WRITE(IP,128) 
  128 FORMAT(/2X,'ALL FREQUENCIES ARE = TO ZERO '/         2X,'ONLY DC A
     +NALYSIS !'/)
  129 CONTINUE
C
C     END OF NAMELIST/FRE/ INPUT  
C  
C
C *** PRINTING INFORMATION FROM NAMELIST/SERV/  
      IF(KPRLEN.GE.1)WRITE(IP, 130) EPSIW,LIMIT,               KPRLEN,KP
     +RSRT,KPRNKR,KPRLIN,               KPRSOL,KPRVAR,KPRGRF,KPRQUP,    
     +           EPSSOL,KITU,KNC
  130 FORMAT(2X,'SERVICE VALUES  :',20X,'FREQUENCIES CMP PRECISION = ',E
     +12.5/20X,'MAX NUMBER OF ITERATIONS  = ',I3/20X,'PRINTINGS     = ',
     +8(I2,',')/20X,'REQUIRED PREC. OF SOLUTION = ',E12.5/20X,'KIND OF I
     +NITIAL APPROXIMATION = ',I2/20X,'NUMBER OF POINTS IN FFT = ',I2/)


C ********** CONTROLLED PRINTING OF ARRAYS ******************  

      IF(KPRLEN.LE.1) GOTO 677

      WRITE(IP,*) 'PRINTS AT OUT OF SUBR LEN'

      WRITE(IP, 610) EPSIW,LIMIT,               KPRLEN,KPRSRT,KPRNKR,KPR
     +LIN,               KPRSOL,KPRVAR,KPRQUP,               EPSSOL,EPSD
     +U,EPSMIN,MAXDU,               KITU,KNC,MGLOB,IAPR
  610 FORMAT(10X,' CONTROL VALUES :',/2X,'EPSIW (1.0E-5) - FREQUENCIES C
     +MP PRECISION = ',E12.5/2X,'LIMIT (500) - MAX NUM OF ITERATIONS = '
     +,I3/2X,'KPRLEN (1) - PRN LEVEL OF INPUT TASK READING = ',I2/2X,'KP
     +RSRT (0) - PRN LEVEL Té ð/ð COPTéPOBKé õúìOB CXEMù = ',I2/2X,'KPRN
     +KR (0) - PRN LEVEL Té ð/ð ðOCTPOEHéñ CETOK þACTOT = ',I2/2X,'KPRLI
     +N (0) - PRN LEVEL Té ð/ð OâPAâOTKé ìéH. ðOäCXEMù = ',I2/2X,'KPRSOL
     + (0) - PRN LEVEL Té ð/ð PEûEH. CéC-Mù HEì. õP-ê = ',I2/2X,'KPRVAR 
     +(1) - PRN LEVEL Té äìñ BAPéAãéé ðAPAMETPOB = ',I2/2X,'KPRQUP (0) -
     + PRN LEVEL ôé ð/ð þôåîéñ ÷è.úáäáîéñ ë.ð. = ',I2/2X,'EPSSOL (1.E-6)
     + - REQUIRED PREC. OF SOLUTION   = ',E12.5/2X,'EPSDU (1.E-6) - MINI
     +MUM ALLOWED STEP = ',E12.5/2X,'EPSMIN (1.E-7) - USED TO DETECT LOC
     +AL MINIMUM ',/2X,'                 WHICH NOT A SOLUTION = ',E12.5/
     +2X,'MAXDU (0.0) - DEF. MAX ALLOWED STEP=',E12.5/2X,'KITU (0) - KIN
     +D OF INITIAL APPROXIMATION = ',I2/2X,'KNC (32) - NUMBER OF POINTS 
     +IN FFT = ',I2/2X,'MGLOB (0) - GLOBALIZATION APPROACH = ',I2/2X,'  
     +          0 - NO GLOBALIZATION',/2X,'            1 - LINEAR SEARCH
     + IN NEWTON DIRECTION',/2X,'IAPR (0) - TURN ON A-PRIORY STEP LIMITA
     +TION  = ',I2/2X,'            0 - OFF 1 - ON   ',/)
C
C *** CONTROLLED PRINTING OF THE ARRAY "MPOINT"  
      WRITE(IP,*) 'MPOINT = '
      DO 612 JJ=1,NMPNT
      N1=20*(JJ-1)+1
      N20=N1+20-1
      WRITE(IP, 611) N1,N20,(MPOINT(N),N=N1,N20)
  611 FORMAT(2X,'(',I3,',',I3,')=',4A4,16(I3))
  612 CONTINUE
C
C *** CONTROLLED PRINTING OF THE ARRAY "NODEEL"  
      NNOD=NNODE-1
      DO 630 JJ=1,NNOD
      WRITE(IP, 620)  JJ, NODEEL(JJ), NODEEL(JJ)
  620 FORMAT(2X,'NODEEL(',I3,')=',I4,2X,A4)
  630 CONTINUE
      WRITE(IP, 633) NNODE
  633 FORMAT(/2X,'FIRST FREE CELL IN  NODEEL - ',I4/)
 
C *** CONTROLLED PRINTING OF THE ARRAY "PARAM"  
      NNP=NPARAM-1
      WRITE(IP, 635) (NN,PARAM(NN),NN=1,NNP)
  635 FORMAT(2X,'PARAM(',I4,')=',E12.5)
      WRITE(IP, 637) NPARAM
  637 FORMAT(/2X,'FIRST FREE CELL IN PARAM - ',I4/)
C
C *** CONTROLLED PRINTING OF THE ARRAY "IFF"  
      DO 650 JF=1,KIFF
      WRITE(IP, 640) JF,(IFF(JJF1,JF),JJF1=1,7)
 640  FORMAT(2X,'IFF(   ,',I3,')=',4A4,3(1X,I3))
 650  CONTINUE
C
C *** CONTROLLED PRINTING OF THE ARRAY "NNIFF"  
      DO 675 IJ=1,KNNIFF
      WRITE(IP, 665) IJ,(NNIFF(I,IJ),I=1,4)
  665 FORMAT(2X,'NNIFF( ,',I2,')=',A4,', ',I2,', ',I4,', ',I3)
  675 CONTINUE
   
C *** CONTROLLED PRINTING OF VARIABLES  
      WRITE(IP, 676) NMPNT,NNODE,NPARAM,LENNOD,          LENPAR,NNETPR,L
     +ENNTP
  676 FORMAT(2X,'NMPNT=',I2,' NNODE=',I3,       1X,'NPARAM=',I4,' LENNOD
     +=',I3/       2X,'LENPAR=',I4,' NNETPR=',I3,       1X,'LENNTP=',I3)
C

  677 CONTINUE

C *** END OF DIRECT INPUT *********************************************
C
C
      RETURN
C
C
C *** ERROR DIAGNOSTICS *********************************************
  140 FORMAT(/1X,78('-')//)
C
  200 CONTINUE
      WRITE(IP, 140)
      WRITE(IP, 210) IT
  210 FORMAT(2X,'TYPE"',4A4,'" IS ABSENT IN  ',          'LIBRARY OF ELE
     +MENTS')
      GOTO 500
C
  220 CONTINUE
      WRITE(IP, 140)
      WRITE(IP, 230) IPAR
  230 FORMAT(2X,'ELEMENT "',A4,'" IS ABSENT IN CKT')
      GOTO 500
C
  240 CONTINUE
      WRITE(IP, 140)
      WRITE(IP, 250) NE
  250 FORMAT(2X,'NAME OF ELEMEN (NE="',A4,'") '/       2X,'SHOULD NOT BE
     + EQUAL TO NAME OF '/       2X,'TYPE OR NAME OF OTHER ELEMENT ')
      GOTO 500
C
  260 CONTINUE
      WRITE(IP, 140)
      WRITE(IP, 270) NE
  270 FORMAT(2X,'FOR ELEMENT ',A4,' IS NOT DEF.'/       2X,'SEMICONDUCTO
     +R SRUCTURE ')
      GOTO 500
C
  280 CONTINUE
      WRITE(IP, 140)
      WRITE(IP, 290)
  290 FORMAT(2X,'LINEAR 2-POLES DO NOT ALLOW IPAR VALUE IN  &ELEM')
      GOTO 500
C
  300 CONTINUE
      WRITE(IP, 140)
      WRITE(IP, 305)
  305 FORMAT(2X,'NO MORE ROOM FOR INPUT')
C IT IS NECESSARY TO INCREASE THE SIZE OF THE STRING "MPOINT" AND 
C ASSIGN A NEW VALUE TO THE VARIABLE LENMPO, COMPARED TO LEN  
      GOTO 500
C
  320 CONTINUE
      WRITE(IP, 140)
      WRITE(IP, 325) NE, KOLPOL
  325 FORMAT(2X,'WRONG NODES NUMBER FOR ELEMENT ',A4,'(',I3,')')
      GOTO 500
C
  330 CONTINUE
      WRITE(IP, 140)
      WRITE(IP, 335) NE, KOLPAR
  335 FORMAT(2X,'WRONG PARAMETERS NUMBER FOR ELEMENT ',A4,'(',I4,')')
      GOTO 500
C
  340 CONTINUE
      WRITE(IP, 140)
      WRITE(IP, 345)
  345 FORMAT( 2X,'CANT USE IPAR IN &ELEM FOR TABLE DEFINED COMPONENT')
      GOTO 500
C
  350 CONTINUE
      WRITE(IP, 140)
      WRITE(IP, 355)
  355 FORMAT(2X,'NO MORE ROOM IN NODEEL')
C IT IS NECESSARY TO INCREASE THE SIZE OF THE STRING NODEEL AND ASSIGN A NEW VALUE  
C TO THE VARIABLE LENNOD, COMPARED TO LEN  
      GOTO 500
C
  360 CONTINUE
      WRITE(IP, 140)
      WRITE(IP, 365)
  365 FORMAT(2X,'NO MORE ROOM IN PARAM')
C IT IS NECESSARY TO INCREASE THE SIZE OF THE STRING "PARAM" AND ASSIGN A NEW VALUE  
C TO THE VARIABLE LENPAR  
      GOTO 500
C
  370 CONTINUE
      WRITE(IP, 140)
      WRITE(IP, 375) KOL
  375 FORMAT(2X,'WRONG NUMBER OF ELEMENTS (KOL=',I4,')'/       2X,'IN &T
     +YP')
      GOTO 500
C
C
C
  500 CONTINUE
      WRITE(IP, 140)
C
      STOP
C
      END
